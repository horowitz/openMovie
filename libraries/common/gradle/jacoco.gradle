apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.12'
    reportsDirectory = file("$buildDir/reports/coverage")
}


ext.setProperty("isCodeAnalysisOn", true)

tasks.withType(Test).configureEach {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    def defaultExclusions = [
            '**/R.*',
            '**/R$*.*',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*_MembersInjector.*',
            '**/di/**/*.*',
            '**/Dagger*Component.*',
            '**/Dagger*Component$Builder.*',
            '**/*Module_*Factory.*',
            '**/*Adapter*.*',
            '**/*ViewPager*.*',
            '**/*ViewHolder*.*',
            '**/*_Provide*/**',
            '**/*_Factory*/**'
    ]

    def moduleExclusions = getModuleExclusions()

    defaultExclusions.addAll(moduleExclusions.split(','))

    // generated classes
    classDirectories.from = fileTree(
            dir: "$buildDir/intermediates/classes/javac/**",
            excludes: defaultExclusions
    ) + fileTree(
            dir: "$buildDir/tmp/kotlin-classes/debug",
            excludes: defaultExclusions
    )
    // sources
    sourceDirectories.from = [
            android.sourceSets.main.java.srcDirs,
            "src/main/kotlin",
            "src/main/java"
    ]
    // Output and existing data
    // Combine Unit test and Instrumented test reports
    executionData.from = fileTree(dir: "$buildDir", includes: [
            // Unit tests coverage data
            "outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec",
            // Instrumented tests coverage data
            "outputs/code_coverage/debugAndroidTest/connected/**/coverage.ec"
    ])

    reports {
        xml.required = true
        html.required = true
    }

    doLast {
        exec { commandLine 'open', "build/reports/coverage/jacocoTestReport/html/index.html" }
    }
}

private String getModuleExclusions() {
    def defaultExclusion = "**/di/**/*,"
    def propertyKey = "module.coverage.exclusions"

    if (hasProperty(propertyKey)) {
        return defaultExclusion + getProperty(propertyKey)
    }
    return defaultExclusion
}